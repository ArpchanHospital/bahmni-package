plugins {
    id "nebula.os-package" version "2.2.6"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://mavenrepo.openmrs.org/nexus/content/repositories/public'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories'
    }
    mavenLocal()
}

group 'org.bahmni.openmrs'
version project.openmrsVersion

apply plugin: 'java'
apply plugin: 'rpm'

task extractWar(dependsOn: 'downloadMRSWar', type: Copy) {
    from zipTree(file("${buildDir}/resources/main/openmrs.war"))
    into file("${buildDir}/openmrs")
}

task normalizeJarName(type: Copy) {
    from "${buildDir}" + "/libs/"
    into "${buildDir}" + "/libs/"
    include(project.name + '-' + project.openmrsVersion + '.jar')
    rename(project.name + '-' + project.openmrsVersion + '.jar', 'openmrs.jar')
}

task dist(dependsOn: ['build', 'normalizeJarName', 'downloadMRSWar'], type: Rpm) {
    packageName 'openmrs'
    release System.getenv('GO_PIPELINE_COUNTER')
    arch NOARCH
    os LINUX
    user 'root'

    into '/opt/openmrs'

    requires('unzip')
    requires('mysql-community-client')

    postInstall file("${projectDir}/scripts/postinstall.sh")
    preUninstall file("${projectDir}/scripts/preuninstall.sh")
    postUninstall file("${projectDir}/scripts/postuninstall.sh")

    from("${buildDir}/libs/openmrs.jar") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'lib/'
    }

    from("${projectDir}/resources/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'etc'
    }

    from("${projectDir}/scripts/rpm/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bin'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'run'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'log'
    }

    from("${buildDir}/resources/main/openmrs.war") {
        fileMode = 0755
        createDirectoryEntry = true
        into '.'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'modules'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'openmrs'
    }

    from("${projectDir}/resources/initDB.sh") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'openmrs/scripts'
    }

    from("${projectDir}/resources/mrs_base.sql") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'openmrs/scripts'
    }
}

task downloadMRSWar << {
    new File("${buildDir}/resources/main").mkdirs();
    configurations.mrsWar.resolve().each { file ->
        if (file.getName().contains("openmrs")) {
            file.renameTo(new File("${buildDir}/resources/main/openmrs.war"))
        }
    }
}

configurations {
    mrsWar
}

downloadMRSWar.onlyIf { !(new File("${buildDir}/resources/main/openmrs.war").exists()) }

dependencies {
    compile project(":core")
    mrsWar "org.openmrs.web:openmrs-webapp:${openmrsVersion}@war"
}

jar {
    manifest {
        attributes 'Main-Class': "org.bahmni.deployment.launch.Main"
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
