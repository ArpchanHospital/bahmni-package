plugins {
    id "nebula.os-package" version "2.2.6"
}

version project.bahmniRelease

apply plugin: 'java'
apply plugin: 'rpm'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://mavenrepo.openmrs.org/nexus/content/repositories/public'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories'
    }
    mavenLocal()
}

task extractWar(type: Copy) {
    from zipTree(file("${projectDir}/resources/pacs-integration.war"))
    into file("${buildDir}/pacs-integration")
}

task normalizeJarName(type: Copy) {
    from "${buildDir}" + "/libs/"
    into "${buildDir}" + "/libs/"
    include(project.name + '-' + project.bahmniRelease + '.jar')
    rename(project.name + '-' + project.bahmniRelease + '.jar', 'pacs-integration.jar')
}

task dist(dependsOn: ['build', 'normalizeJarName', 'extractWar'], type: Rpm) {
    packageName 'bahmni-pacs'
    release System.getenv('GO_PIPELINE_COUNTER')
    arch NOARCH
    os LINUX
    user 'root'

    into '/opt/bahmni-pacs'

    requires('postgresql92')

    postInstall file("${projectDir}/scripts/postinstall.sh")
    preUninstall file("${projectDir}/scripts/preuninstall.sh")
    postUninstall file("${projectDir}/scripts/postuninstall.sh")

    from("${projectDir}/scripts/rpm/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bin'
    }

    from("${projectDir}/resources") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'etc'
    }

    from("${buildDir}/libs/pacs-integration.jar") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'lib'
    }

    from("${buildDir}/pacs-integration") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'pacs-integration'
    }
}

dependencies {
    compile project(":core")
}

jar {
    manifest {
        attributes 'Main-Class': "org.bahmni.deployment.launch.Main"
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
