plugins {
    id "nebula.os-package" version "2.2.6"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://mavenrepo.openmrs.org/nexus/content/repositories/public'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories'
    }
    mavenLocal()
}

group 'org.bahmni.lab'
version '0.77'

project.ext.jarBaseName = 'bahmni-lab'
project.ext.openelisVersion = "0.77";

apply plugin: 'java'
apply plugin: 'rpm'

//The openelis.war & OpenElis.zip file will be copied by the CI server after the artifacts are built.
task extractWar(type: Copy) {
    from zipTree(file("${projectDir}/resources/deployables/openelis.war"))
    into file("${buildDir}/bahmni-lab")
}

task extractMigrations(type: Copy) {
    from zipTree(file("${projectDir}/resources/deployables/OpenElis.zip"))
    into file("${buildDir}/migrations")
}

task normalizeJarName(type: Copy) {
    from "${buildDir}" + "/libs/"
    into "${buildDir}" + "/libs/"
    include("${jarBaseName}" + '-' + "${openelisVersion}" + '.jar')
    rename("${jarBaseName}" + '-' + "${openelisVersion}" + '.jar', 'bahmni-lab.jar')
}

task dist(dependsOn: ['build', 'normalizeJarName', 'extractWar', 'extractMigrations'], type: Rpm) {
    println("The value of GO_PIPELINE_COUNTER is "+ System.getProperty('GO_PIPELINE_COUNTER', 'Unknown'));
    packageName 'bahmni-lab'
    release System.getenv('GO_PIPELINE_COUNTER')
    arch NOARCH
    os LINUX
    user 'root'

    into '/opt/bahmni-lab'

    requires('unzip')
    requires('postgresql')

    postInstall file("${projectDir}/scripts/postinstall.sh")
    preUninstall file("${projectDir}/scripts/preuninstall.sh")
    postUninstall file("${projectDir}/scripts/postuninstall.sh")

    from("${projectDir}/resources/log4j.xml") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bahmni-lab/WEB-INF/classes'
    }

    from("${buildDir}/libs/bahmni-lab.jar") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'lib/'
    }

    from("${projectDir}/resources/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'etc'
    }

    from("${projectDir}/scripts/rpm/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bin'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'run'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'log'
    }

    from("${buildDir}/bahmni-lab/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bahmni-lab'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bahmni-lab'
    }

    from("${buildDir}/migrations/OpenElis") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'migrations'
        include('db_backup/*')
        include('liquibase/*')
        include('liquibase/lib/*')
        include('liquibase/Bahmni/*')
        include('scripts/*')
    }

    from("${projectDir}/resources/migrateDb.sh") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'migrations/scripts/'
    }
}

dependencies {
    compile project(":core")
}

jar {
    manifest {
        attributes 'Main-Class': "org.bahmni.openmrs.launch.Main"
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
